#!/usr/bin/env zsh

set -e
set -u
set -o pipefail

TEST_DATA='Hello, world!'

if [[ $# -ne 1 ]]; then
  echo >&2 "Usage: ${0} IMAGE"
  exit 2
fi

IMAGE="$1"

function main {
    start
    trap stop EXIT TERM
    sleep 1

    if [[ "$(curl "$(docker-hostname):$(docker-port "$CONTAINER" 8080)")" == "$TEST_DATA" ]]; then
        echo >&2 'Test passed.'
        exit 0
    else
        echo >&2 'Test failed.'
        exit 1
    fi
}

function start {
    CONTAINER="$(docker run --detach --publish=8080 "$IMAGE" echo "$TEST_DATA")"
}

function stop {
    local exit_status=$?
    if [[ -n "$CONTAINER" ]]; then
        docker stop "$CONTAINER"
        local container_exit_status="$(docker wait "$CONTAINER")"
        docker rm "$CONTAINER"
        if [[ $exit_status -eq 0 && $container_exit_status -ne 0 ]]; then
            echo >&2 "Container exited with $container_exit_status."
            exit 1
        fi
    fi
    exit $exit_status
}

function docker-hostname {
  if (( ${+DOCKER_HOST} )) && [[ "${DOCKER_HOST:0:6}" == 'tcp://' ]]; then
    echo "$DOCKER_HOST" | sed -E 's#^tcp://([^:]+):[0-9]+#\1#'
  else
    echo 'localhost'
  fi
}

function docker-port {
  docker port "$1" "$2" | sed -E 's/^.+?://'
}

main
